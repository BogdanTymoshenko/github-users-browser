buildscript {
    ext.support_lib_version = '25.3.1'
    ext.kotlin_version = '1.1.1'
    ext.retrofit_version = '2.2.0'
    ext.okhttp_version = '3.6.0'
    ext.rxbinding_version = '1.0.1'
    ext.dagger_version = '2.10'
    ext.junit_version = '4.12'

    ext.espresso_version = '2.2.2'
    ext.runnerVersion = '0.4.1'

    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath "io.fabric.tools:gradle:1.21.6"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.amicablesoft.ghusersbrowser.android"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
         unitTests.returnDefaultValues = true
    }
}

kapt {
    generateStubs = true
}

dependencies {
    // Kotlin stdlib
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Support libraries
    compile "com.android.support:appcompat-v7:$support_lib_version"
    compile "com.android.support:design:$support_lib_version"
    compile "com.android.support:recyclerview-v7:$support_lib_version"
    compile "com.android.support:cardview-v7:$support_lib_version"
    compile "com.android.support.test.espresso:espresso-idling-resource:$espresso_version"

    // Rest API retrofit+okhttp
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava:$retrofit_version"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Rx stuff
    compile 'io.reactivex:rxjava:1.2.9'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:$rxbinding_version"

    // DI
    compile "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Image loader
    compile 'com.squareup.picasso:picasso:2.5.2'

    // Testing
    testCompile "junit:junit:$junit_version"
    testCompile 'org.mockito:mockito-core:2.7.20'
    testCompile ('com.nhaarman:mockito-kotlin:1.3.0', {
        exclude group: 'org.jetbrains.kotlin'
    })
    kaptTest "com.google.dagger:dagger-compiler:$dagger_version"

    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:$runnerVersion"
    androidTestCompile "com.android.support.test:rules:$runnerVersion"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile ("com.android.support.test.espresso:espresso-contrib:$espresso_version", {
        exclude group: 'com.android.support'//, module: 'support-annotations'
    })
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espresso_version"

    // Resolve conflicts between main and test APK:
    androidTestCompile "com.android.support:support-annotations:$support_lib_version"
    androidTestCompile "com.android.support:support-v4:$support_lib_version"
    androidTestCompile "com.android.support:recyclerview-v7:$support_lib_version"
}
